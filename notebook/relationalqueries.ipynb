{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Relational queries\n",
    "\n",
    "Alejandro Morales\n",
    "\n",
    "Centre for Crop Systems Analysis - Wageningen University\n",
    "\n",
    "In this example we illustrate how to test relationships among nodes inside queries.\n",
    "Relational queries allow to establish relationships between nodes in the graph,\n",
    "which generally requires a intimiate knowledge of the graph. For this reason,\n",
    "relational queries are inheretly complex as graphs can become complex and there\n",
    "may be solutions that do not require relational queries in many instances.\n",
    "Nevertheless, they are integral part of VPL and can sometimes be useful. As they\n",
    "can be hard to grasp, this tutorial will illustrate with a relatively simple\n",
    "graph a series of relational queries with increasing complexity with the aim\n",
    "that users will get a better understanding of relational queries. For this purpose,\n",
    "an abstract graph with several branching levels will be used, so that we can focus\n",
    "on the relations among the nodes without being distracted by case-specific details.\n",
    "\n",
    "The graph will be composed of two types of nodes: the inner nodes (`A` and `C`) and the\n",
    "leaf nodes (`B`). Each leaf node will be identified uniquely with an index and\n",
    "the objective is to write queries that can identify a specific subset of the leaf\n",
    "nodes, without using the data stored in the nodes themselves. That is, the queries\n",
    "should select the right nodes based on their relationships to the rest of nodes\n",
    "in the graph. Note that `C` nodes contain a single value that may be positive or negative,\n",
    "whereas `A` nodes contain no data.\n",
    "\n",
    "As usual, we start with defining the types of nodes in the graph"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using VirtualPlantLab\n",
    "import GLMakie\n",
    "\n",
    "module Queries\n",
    "    using VirtualPlantLab\n",
    "    struct A <: Node end\n",
    "\n",
    "    struct C <: Node\n",
    "        val::Float64\n",
    "    end\n",
    "\n",
    "    struct B <: Node\n",
    "        ID::Int\n",
    "    end\n",
    "end\n",
    "import .Queries as Q"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "We generate the graph directly, rather than with rewriting rules. The graph has\n",
    "a motif that is repeated three times (with a small variation), so we can create\n",
    "the graph in a piecewise manner. Note how we can use the function `sum` to add\n",
    "nodes to the graph (i.e. `sum(A() for i in 1:3)` is equivalent to `A() + A() + A()`)"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "motif(n, i = 0) = Q.A() + (Q.C(45.0) + Q.A() + (Q.C(45.0) +  Q.A() + Q.B(i + 1),\n",
    "                                           Q.C(-45.0) + Q.A() + Q.B(i + 2),\n",
    "                                                       Q.A() + Q.B(i + 3)),\n",
    "                         Q.C(- 45.0) + sum(Q.A() for i in 1:n) + Q.B(i + 4))\n",
    "axiom =  motif(3, 0) + motif(2, 4) + motif(1, 8) + Q.A() + Q.A() + Q.B(13)\n",
    "graph = Graph(axiom = axiom);\n",
    "draw(graph)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "By default, VPL will use as node label the type of node and the internal ID generated by VPL itself. This ID is useful if we want to\n",
    "extract a particular node from the graph, but it is not controlled by the user. However, the user can specialized the function `node_label()`\n",
    "to specify exactly how to label the nodes of a particular type. In this case, we want to just print `A` or `C` for nodes of type `A` and `C`, whereas\n",
    "for nodes of type `B` we want to use the `ID` field that was stored inside the node during the graph generation."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "VirtualPlantLab.node_label(n::Q.B, id) = \"B-$(n.ID)\"\n",
    "VirtualPlantLab.node_label(n::Q.A, id) = \"A\"\n",
    "VirtualPlantLab.node_label(n::Q.C, id) = \"C\"\n",
    "draw(graph)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "To clarify, the `id` argument of the function `node_label()` refers to the internal id generated by VPL (used by the default method for `node_label()`, whereas the the first argument is the data stored inside a node (in the case of `B` nodes, there is a field called `ID` that will not be modified by VPL as that is user-provided data).\n",
    "\n",
    "The goal of this exercise is then to write queries that retrieve specific `B`\n",
    "nodes  without using the data stored in the node in the query. That is, we have\n",
    "to identify nodes based on their relationships to other nodes.\n",
    "\n",
    "## All nodes of type `B`\n",
    "\n",
    "First, we create the query object. In this case, there is no special condition as\n",
    "we want to retrieve all the nodes of type `B`"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "Q1 = Query(Q.B)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Applying the query to the graph returns an array with all the `B` nodes"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "A1 = apply(graph, Q1)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "For the remainder of this tutorial, the code will be hidden by default to allow users to try on their own.\n",
    "\n",
    "\n",
    "## Node containing value 13\n",
    "\n",
    "Since the `B` node 13 is the leaf node of the main branch of the graph (e.g. this could be the apical meristem of the main stem of a plant), there\n",
    "are no rotations between the root node of the graph and this node. Therefore,\n",
    "the only condition require to single out this node is that it has no ancestor\n",
    "node of type `C`.\n",
    "\n",
    "Checking whether a node has an ancestor that meets a certain\n",
    "condition can be achieved with the function `hasAncestor()`. Similarly to the\n",
    "condition of the `Query` object, the `hasAncestor()` function also has a condition,\n",
    "in this case applied to the parent node of the node being tested, and moving\n",
    "upwards in the graph recursively (until reaching the root node). Note that, in\n",
    "order to access the object stored inside the node, we need to use the `data()`\n",
    "function, and then we can test if that object is of type `C`. The `B` node 13\n",
    "is the only node for which `hasAncestor()` should return `false`:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function Q2_fun(n)\n",
    "    check, steps = has_ancestor(n, condition = x -> data(x) isa Q.C)\n",
    "    !check\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "As before, we just need to apply the `Query` object to the graph:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "Q2 = Query(Q.B, condition = Q2_fun)\n",
    "A2 = apply(graph, Q2)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Nodes containing values 1, 2 and 3\n",
    "\n",
    "These three nodes belong to one of the branch motifs repeated through the graph. Thus,\n",
    "we need to identify the specific motif they belong to and chose all the `B` nodes\n",
    "inside that motif. The motif is defined by an `A` node that has a `C` child with\n",
    "a negative `val` and parent node `C` with positive `val`. This `A` node\n",
    "should then be 2 nodes away from the root node to separate it from upper repetitions\n",
    "of the motif.\n",
    "Therefore, we need to test for two conditions, first find those nodes inside a\n",
    "branch motif, then retrieve the root of the branch motif (i.e., the `A` node\n",
    "described in the above) and then check the distance of that node from the root:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function branch_motif(p)\n",
    "    data(p) isa Q.A &&\n",
    "    has_descendant(p, condition = x -> data(x) isa Q.C && data(x).val < 0.0)[1] &&\n",
    "    has_ancestor(p, condition = x -> data(x) isa Q.C && data(x).val > 0.0)[1]\n",
    "end\n",
    "\n",
    "function Q3_fun(n, nsteps)\n",
    "    # Condition 1\n",
    "    check, steps = has_ancestor(n, condition = branch_motif)\n",
    "    !check && return false\n",
    "    # Condition 2\n",
    "    p = parent(n, nsteps = steps)\n",
    "    check, steps = has_ancestor(p, condition = is_root)\n",
    "    steps != nsteps && return false\n",
    "    return true\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "And applying the query to the object results in the required nodes:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "Q3 = Query(Q.B, condition = n -> Q3_fun(n, 2))\n",
    "A3 = apply(graph, Q3)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Node containing value 4\n",
    "\n",
    "The node `B` with value 4 can be singled-out because there is no branching point\n",
    "between the root node and this node. This means that no ancestor node should have\n",
    "more than one children node except the root node. Remember that `hasAncestor()`\n",
    "returns two values, but we are only interested in the first value. You do not need to\n",
    "assign the returned object from a Julia function, you can just index directly the element\n",
    "to be selected from the returned tuple:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function Q4_fun(n)\n",
    "    !has_ancestor(n, condition = x -> is_root(x) && length(children(x)) > 1)[1]\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "And applying the query to the object results in the required node:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "Q4 = Query(Q.B, condition = Q4_fun)\n",
    "A4 = apply(graph, Q4)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Node containing value 3\n",
    "\n",
    "This node is the only `B` node that is four steps from the root node, which we can\n",
    "retrieve from the second argument returned by `hasAncestor()`:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function Q5_fun(n)\n",
    "    check, steps = has_ancestor(n, condition = is_root)\n",
    "    steps == 4\n",
    "end\n",
    "\n",
    "Q5 = Query(Q.B, condition = Q5_fun)\n",
    "A5 = apply(graph, Q5)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Node containing value 7\n",
    "\n",
    "Node `B` 7 belongs to the second lateral branch motif and the second parent\n",
    "node is of type `A`. Note that we can reuse the `Q3_fun` from before in the\n",
    "condition required for this node:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function Q6_fun(n, nA)\n",
    "    check = Q3_fun(n, nA)\n",
    "    !check && return false\n",
    "    p2 = parent(n, nsteps = 2)\n",
    "    data(p2) isa Q.A\n",
    "end\n",
    "\n",
    "Q6 = Query(Q.B, condition = n -> Q6_fun(n, 3))\n",
    "A6 = apply(graph, Q6)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Nodes containing values 11 and 13\n",
    "\n",
    "The `B` nodes 11 and 13 actually have different relationships to the rest of the graph,\n",
    "so we just need to define two different condition functions and combine them.\n",
    "The condition for the `B` node 11 is similar to the `B` node 7, whereas the condition\n",
    "for node 13 was already constructed before, so we just need to combined them with an\n",
    "OR operator:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "Q7 = Query(Q.B, condition = n -> Q6_fun(n, 4) || Q2_fun(n))\n",
    "A7 = apply(graph, Q7)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Nodes containing values 1, 5 and 9\n",
    "\n",
    "These nodes play the same role in the three lateral branch motifs. They are the\n",
    "only `B` nodes preceded by the sequence A C+ A. We just need to check the\n",
    "sequence og types of objects for the the first three parents of each `B` node:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function Q8_fun(n)\n",
    "    p1 = parent(n)\n",
    "    p2 = parent(n, nsteps = 2)\n",
    "    p3 = parent(n, nsteps = 3)\n",
    "    data(p1) isa Q.A && data(p2) isa Q.C && data(p2).val > 0.0 && data(p3) isa Q.A\n",
    "end\n",
    "\n",
    "Q8 = Query(Q.B, condition = Q8_fun)\n",
    "A8 = apply(graph, Q8)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Nodes contaning values 2, 6 and 10\n",
    "\n",
    "This exercise is similar to the previous one, but the C node has a negative\n",
    "`val`. The problem is that node 12 would also match the pattern A C- A. We\n",
    "can differentiate between this node and the rest by checking for a fourth\n",
    "ancestor node of class `C`:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function Q9_fun(n)\n",
    "    p1 = parent(n)\n",
    "    p2 = parent(n, nsteps = 2)\n",
    "    p3 = parent(n, nsteps = 3)\n",
    "    p4 = parent(n, nsteps = 4)\n",
    "    data(p1) isa Q.A && data(p2) isa Q.C && data(p2).val < 0.0 &&\n",
    "       data(p3) isa Q.A && data(p4) isa Q.C\n",
    "end\n",
    "\n",
    "Q9 = Query(Q.B, condition = Q9_fun)\n",
    "A9 = apply(graph, Q9)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Nodes containg values 6, 7 and 8\n",
    "\n",
    "We already came up with a condition to extract node 7. We can also modify the previous\n",
    "condition so that it only node 6.  Node 8 can be identified by checking for the third\n",
    "parent node being of type `C` and being 5 nodes from the root of the graph.\n",
    "\n",
    "As always, we can reusing previous conditions since they are just regular Julia functions:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function Q10_fun(n)\n",
    "    Q6_fun(n, 3) && return true ## Check node 7\n",
    "    Q9_fun(n) && has_ancestor(n, condition = is_root)[2] == 6 && return true ## Check node 6\n",
    "    has_ancestor(n, condition = is_root)[2] == 5 && data(parent(n, nsteps = 3)) isa Q.C && return true ## Check node 8 (and not 4!)\n",
    "end\n",
    "\n",
    "Q10 = Query(Q.B, condition = Q10_fun)\n",
    "A10 = apply(graph, Q10)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Nodes containig values 3, 7, 11 and 12\n",
    "\n",
    "We already have conditions to select nodes 3, 7 and 11 so we just need a new condition\n",
    "for node 12 (similar to the condition for 8)."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function Q11_fun(n)\n",
    "    Q5_fun(n) && return true ## 3\n",
    "    Q6_fun(n, 3) && return true ## 7\n",
    "    Q6_fun(n, 4) && return true ## 11\n",
    "    has_ancestor(n, condition = is_root)[2] == 5 && data(parent(n, nsteps = 2)) isa Q.C &&\n",
    "        data(parent(n, nsteps = 4)) isa Q.A && return true ## 12\n",
    "end\n",
    "\n",
    "Q11 = Query(Q.B, condition = Q11_fun)\n",
    "A11 = apply(graph, Q11)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Nodes containing values 7 and 12\n",
    "\n",
    "We just need to combine the conditions for the nodes 7 and 12"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function Q12_fun(n)\n",
    "    Q6_fun(n, 3) && return true # 7\n",
    "    has_ancestor(n, condition = is_root)[2] == 5 && data(parent(n, nsteps = 2)) isa Q.C &&\n",
    "        data(parent(n, nsteps = 4)) isa Q.A && return true ## 12\n",
    "end\n",
    "\n",
    "Q12 = Query(Q.B, condition = Q12_fun)\n",
    "A12 = apply(graph, Q12)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.3"
  },
  "kernelspec": {
   "name": "julia-1.9",
   "display_name": "Julia 1.9.3",
   "language": "julia"
  }
 },
 "nbformat": 4
}
